# vim:se syn=tcl:
#
# Example auto.def which shows how to use various commands
# and features.
#
autosetup-require-version 0.6.7

# This is deprecated
#define defaultprefix /usr

# option-defaults can be used to change the defaults for module options
# set this before 'use'
# Note that the help text is not updated for boolean options
options-defaults {
	silent-rules 1
	prefix /usr
}

use cc cc-lib cc-db pkg-config

options {
	# Comment lines are allowed in options
	shared    => "Shared library"
	utf8=1    => "utf8 support"
	# --lfs is a synonym for --largefile
	lfs=1 largefile=1 => "Large File Support"
	opt1:value => "Option with a value (default=@default@)"
	opt2:path=/usr => "Option with a default value (default=@default@)"
	opt3:=value => "Option with an optional value (default=@default@)"
}

# Argument handling. If opt2 isn't specified, it will have the default value of /usr
foreach opt {opt1 opt2 opt3} {
	if {[opt-str $opt o]} {
		puts "Option $opt has value $o"
	}
}

# In general, the cc-check-* commands return true
# if all the tests succeed.
if {[cc-check-types "long long"]} {
	cc-check-sizeof "long long"
}

# Create our own test for 'volatile'
msg-checking "Checking for volatile..."
if {[cctest -code {volatile int _x = 0;}]} {
	msg-result ok
	define-feature volatile
} else {
	msg-result no
	define-feature volatile 0
}

# Or even simpler. Compile the code and define 'const' appropriately
check-feature const {
	cctest -code {const int _x = 0;}
}

# Does the C compiler understand -std=c99?
check-feature -std=c99 {
	cctest -cflags {-std=c99}
}
# Could check the return code of check-feature instead
if {[have-feature -std=c99]} {
	define-append-argv AS_CFLAGS -std=c99 -fpedantic {-Dversion="my version"}
}

# or use cc-check-flags if you want working flags added to AS_CFLAGS automatically
cc-check-flags -bogus

# But use -Werror if you are testing a warning flag
cc-with {-cflags -Werror} {
	cc-check-flags -Wimplicit-fallthrough=5 -Wformat-security
}
# Or note that any diagnostics from the compiler means it isn't supported
cc-with {-nooutput 1} {
	cc-check-flags -time
}

# When checking for linker flags, need to explicitly link
# And we don't want them added to AS_CFLAGS automatically
define-push {AS_CFLAGS} {
	cc-with {-link 1} {
		cc-check-flags -Wl,--as-needed -Wl,--no-undefined -Wl,--rpath,/tmp -Wl,--bogus-link-flag
	}
}

define-append-argv version1 "with spaces"

# Many apps want to know if they are building with gcc (or at least a compatible compiler)
cc-check-decls __GNUC__

cc-check-includes netinet/in.h

cc-check-progs grep badcommand
cc-path-progs ls badcommand2

# autosetup will check for the existence of any include files
# mentioned in -includes before including them, silently excluding
# any which don't exist.
#
cc-with {-includes {sys/socket.h blah/blah.h}} {
	cc-check-members "struct msghdr.msg_control"
}
cc-with {-includes sys/stat.h} {
	cc-check-members "struct stat.st_mtime" "struct stat.st_atime" "struct stat.st_size"
}


# These should prepend to LIBS
cc-check-function-in-lib yyerror y
cc-check-function-in-lib sin m
cc-check-function-in-lib dlopen dl
cc-check-function-in-lib percent_m wrap

# Checking the same thing twice is automatically cached
cc-check-includes sys/socket.h sys/stat.h

if {[opt-bool lfs largefile]} {
	cc-check-lfs
}
cc-check-endian

# Now try some C++ features
# Does the C compiler understand -fno-rtti?
check-feature -fno-rtti {
	cctest -lang c++ -cflags -fno-rtti
}

# What about general features using the c++ compiler?
# First C
cc-with {-cflags -std=c89} {
	cc-check-types bool
}
# Now C++
cc-with {-lang c++} {
	cc-check-types bool
	cc-check-c11
}

# libs are in reverse (nested) order
# libs will be in this order: -ldl -lc -lm
cc-with {-libs {-lc -lm}} {
	cc-with {-libs -ldl} {
		cc-check-functions dlopen
	}
}

# While -includes and -cflags are in the order given
cc-with {-cflags -DX=1} {
	cc-with {-cflags -UX} {
		check-feature flags-order {
			cctest -code {
				#ifdef X
				#error X should not be defined
				#endif
			}
		}
	}
}

cc-with {-nooutput 1} {
	check-feature warning-as-error {
		cctest -code {
			#warning This warning should be treated as an error
		}
	}
}
check-feature warning-ignored {
	cctest -code {
		#warning This warning should be ignored
	}
}

# cc-check-decls recognises both defines and rvalues
cc-check-includes stdio.h
cc-with {-cflags -DABC -includes stdio.h} {
	cc-check-decls ABC fopen
}

# some things which definitely won't exist
cc-check-functions blahfunc
cc-check-decls blahdecl
cc-check-defines blahdef
cc-check-types blahtype
cc-check-sizeof blahtype

define _TEST
define PACKAGE_NAME testbed
define PACKAGE_VERSION 1.0

define TRUEVAL
define FALSEVAL 0
define EMPTYVAL ""
define STRVAL strval

# And apply these settings for the rest of the file
cc-with {-lang c++}

# Test pkg-config, where pkg-config support is optional
if {[pkg-config-init 0]} {
	if {[pkg-config pango >= 1.37.0]} {
		msg-result "Checking for pango libdir ...[pkg-config-get-var pango libdir]"
	}
	if {[pkg-config zlib]} {
		msg-result "Checking for zlib libdir ...[pkg-config-get-var zlib libdir]"
	}
	pkg-config nonexistent
}

make-config-header config.h -auto "_* PACKAGE_*"
make-template Makefile.in
# And a template in a subdir too
make-template subdir/Makefile.in

# For illustration purposes, create a template containing all known variables

set f [open template.in w]
# We want these sorted.
foreach n [lsort [dict keys [all-defines]]] {
	puts $f "$n=@$n@"
}
close $f

# And process the template. Note that we need to explictly
# specify that the template is in the build directory, not
# the source directory
make-template [get-define builddir]/template.in
